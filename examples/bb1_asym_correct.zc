adversary A1 : () -> (Fq * G_t).
bilinear map e : G_1 * G_2 -> G_t.

prove
  [ (* master secret/public key *)
    alpha <-$ Fq;
    beta  <-$ Fq;
    delta <-$ Fq;
    let g1  = g_1^alpha;
    let h1  = g_1^delta;
    let g1' = g_2^alpha;
    let h1' = g_2^delta;
    let g0' = g_2^(alpha*beta);
    let v = e(g_1,g0');
    (i,m) <- A1();
    (* cipher *)
    s <-$ Fq;
    let c1 = m*v^s;
    let c2 = g_1^s;
    let c3 = (g1^i*h1)^s;
    (* key for identity i *)
    r <-$ Fq;
    let k1 = g0'*(g1'^i*h1')^r;
    let k2 = g_2^r;
    (* decryption *)
    let m' = c1*e(c3,k2)*e(c2,k1)^(-1);
  ] : not (m = m').
  rnorm. rfalse_ev.
  
  extract "bb1_asym_correct.ec".